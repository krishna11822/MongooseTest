SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
IF OBJECT_ID('dbo.RegenerateSiteViewSp') IS NOT NULL
    DROP PROCEDURE dbo.RegenerateSiteViewSp
GO
/* $Header: ApplicationDB\Stored Procedures\RegenerateSiteViewSp.sp 10.1.0.1 03/30/2018 14:27:36 */

/*
***************************************************************
*                                                             *
*                           NOTICE                            *
*                                                             *
*   THIS SOFTWARE IS THE PROPERTY OF AND CONTAINS             *
*   CONFIDENTIAL INFORMATION OF INFOR AND/OR ITS AFFILIATES   *
*   OR SUBSIDIARIES AND SHALL NOT BE DISCLOSED WITHOUT PRIOR  *
*   WRITTEN PERMISSION. LICENSED CUSTOMERS MAY COPY AND       *
*   ADAPT THIS SOFTWARE FOR THEIR OWN USE IN ACCORDANCE WITH  *
*   THE TERMS OF THEIR SOFTWARE LICENSE AGREEMENT.            *
*   ALL OTHER RIGHTS RESERVED.                                *
*                                                             *
*   (c) COPYRIGHT 2018 INFOR.  ALL RIGHTS RESERVED.           *
*   THE WORD AND DESIGN MARKS SET FORTH HEREIN ARE            *
*   TRADEMARKS AND/OR REGISTERED TRADEMARKS OF INFOR          *
*   AND/OR ITS AFFILIATES AND SUBSIDIARIES. ALL RIGHTS        *
*   RESERVED.  ALL OTHER TRADEMARKS LISTED HEREIN ARE         *
*   THE PROPERTY OF THEIR RESPECTIVE OWNERS.                  *
*                                                             *
***************************************************************
*/

/* $Archive: /Tools/SQLScripts/ApplicationDB/Stored Procedures/RegenerateSiteViewSp.sp $
 * CoreDev 18 MGD-3683 nelaba Mon Oct 26 2021
 * Fix on checking for invalid site column. Made sure site column is validated only when it exists in AppTable.
 *
 * CoreDev 17 MGD-1292 nelaba Mon May 03 2021
 * Return an error message when site column from AppTable does not exist in its actual table.
 *
 * CoreDev 16 248405 DJohnson Thu Oct 04
 * Add dbo. schema to path where missing.
 *
 * CoreDev 15 216047 Mmarsolo Wed Jul 20 12:00:31 2016
 * Remediate SQL Injection Risk for RC4 List
 * 216047 - Removed conditional variable from parameters.
 *
 * CoreDev 14 216047 Mmarsolo Tue Jul 19 13:18:09 2016
 * Remediate SQL Injection Risk for RC4 List
 * 217047 - SQL injection remediation, use QUOTENAME and sp_executesql.
 *
 * CoreDev 13 176344 Jray Wed Mar 19 16:34:04 2014
 * RegenerateSiteViewsSp fails when attempting to use it in conjunction with a schema change that impacts the parms table.
 * Issue176344: Modified RegenerateSiteViewSp to eliminate dependency on parms view
 *
 * CoreDev 12 169300 Jray Wed Sep 25 10:42:33 2013
 * RegenerateSiteViewSp needs brackets to handle SQL tables that use reserved SQL words
 * Issue 169300: Add brackets surrounding table and column names in generated view
 *
 * CoreDev 11 168769 Jray Thu Sep 12 10:58:00 2013
 * Error creating demo DB in SL9 RC5
 * Issue 168769: Modifled to add "ORDER BY sc.column_id" to ensure ordering of columns match between tables and views
 *
 * CoreDev 10 168241 Djohnson Wed Sep 04 13:33:24 2013
 * Views over mst tables have site_ref column in them.
 * Issue #168241 - Specifically list columns in the view instead of using the wildcard and exclude the site_ref column non _all tables.
 *
 * CoreDev 9 RS5905 Djohnson Tue Mar 12 12:17:02 2013
 * RS5905 - move the parms ref to the top so when parms view is dropped it does not blow.
 *
 * CoreDev 8 RS5905 Djohnson Fri Mar 08 14:31:15 2013
 * RS5905 - _all views are on base table if all non external sites are in the same db.
 *
 * CoreDev 7 RS5905 Lzhan Fri Feb 22 02:50:22 2013
 * RS5905: added logic for share table.
 *
 * CoreDev 6 RS5905 Djohnson Mon Feb 04 14:32:26 2013
 * RS5905 - Intranet shared users processing.
 *
 * CoreDev 4 RS5905 Jray Mon Dec 17 15:03:32 2012
 * RS5905: Remove unnecessary RTRIM
 *
 * CoreDev 3 RS5905 Djohnson Wed Dec 05 16:15:37 2012
 * Fix typo.
 *
 * CoreDev 2 RS5905 Djohnson Wed Dec 05 16:04:26 2012
 * New parameter from SyteLine requirement to filter or not on site column.
 *
 * CoreDev 1 RS5905 Djohnson Wed Oct 31 17:41:11 2012
 * RS5905 - recreate a site split view.
 * $NoKeywords: $
 */
CREATE PROCEDURE dbo.RegenerateSiteViewSp (
  @TableName TableNameType
, @Infobar   InfobarType OUTPUT
, @IncludeSiteRefFilter ListYesNoType = 1
)
AS
BEGIN

   -- Check for existence of Generic External Touch Point routine (this section was generated by SpETPCodeSp and inserted by CallETPs.exe):
   IF OBJECT_ID(N'dbo.EXTGEN_RegenerateSiteViewSp') IS NOT NULL
   BEGIN
      DECLARE @EXTGEN_SpName sysname
      SET @EXTGEN_SpName = N'dbo.EXTGEN_RegenerateSiteViewSp'
      -- Invoke the ETP routine, passing in (and out) this routine's parameters:
      DECLARE @EXTGEN_Severity int
      EXEC @EXTGEN_Severity = @EXTGEN_SpName
        @TableName
      , @Infobar OUTPUT
      , @IncludeSiteRefFilter

      -- ETP routine can RETURN 1 to signal that the remainder of this standard routine should now proceed:
      IF @EXTGEN_Severity <> 1
         RETURN @EXTGEN_Severity
   END
   -- End of Generic External Touch Point code.
 
   DECLARE
    @Severity   INT = 0
  , @SQL        NVARCHAR(MAX)
  , @ViewName   TableNameType
  , @SiteColumn ColumnNameType
  , @IsView     tinyint = 0
  , @BaseTable  TableNameType = NULL
  , @Columns    NVARCHAR(MAX)
  , @MasterSite SiteType

   /* Check to see if @TableName is a view and not a "mst_all" table */
   /* Only optionally-shareable "mst" "user" table could be a view   */
   /* if shared & not in application db containing master site       */

   SELECT @IsView = 1
      FROM sys.views
      WHERE name = @TableName
        AND @TableName NOT LIKE '%_mst_all'

   IF @IsView = 1 /* @TableName is a shared user table, find & set @MasterSite */
   BEGIN
      SELECT TOP 1 @MasterSite = IT.MasterSite
         FROM site
         INNER JOIN intranet IT ON IT.intranet_name = site.intranet_name
  	   WHERE IT.MasterSite IS NOT NULL
        AND site.site = dbo.GetSiteContext()
        AND site.app_db_name = DB_NAME()
   END
   ELSE /* @TableName is a table, check to see if there is a row in           */
        /* IntranetSharedUserTable with Shared & Processed = 1 for @TableName */
        /* and if so, find & set @MasterSite                                  */
   BEGIN
      SELECT TOP 1 @MasterSite = MasterSite
         FROM IntranetSharedUserTable IST 
            INNER JOIN Intranet IT ON IST.IntranetName = IT.intranet_name
  	      WHERE IST.TableName = @TableName AND IST.Shared = 1 AND IST.Processed = 1
   END

   IF @TableName like '%_all'
      SET @BaseTable = SUBSTRING(@TableName, 1, LEN(@TableName) - 4)

   SELECT
     @ViewName   = AppViewName
   , @SiteColumn = SiteColumnName
   FROM AppTable
   WHERE TableName = @TableName

   /* Check if the @SiteColumn is a valid column */
   IF @IncludeSiteRefFilter = 1 AND @SiteColumn IS NOT NULL
   BEGIN
	   IF NOT EXISTS (SELECT COLUMN_NAME FROM INFORMATION_SCHEMA.columns 
					WHERE TABLE_SCHEMA = 'dbo' AND 
						TABLE_NAME = @TableName AND 
						COLUMN_NAME = @SiteColumn)
	   BEGIN
	      /* Return error message when @SiteColumn is invalid. */
		  EXEC @Severity = dbo.MsgAppSp 
		    @Infobar OUTPUT
		   , 'E=ColumnFromTableNotExist'
		   , @SiteColumn
           , @TableName
	      RETURN @Severity
	   END			
   END

   IF @ViewName IS NULL
   BEGIN
      EXEC @Severity = dbo.MsgAppSp 
       @Infobar OUTPUT
      , 'E=NoExist1'
      , '@AppTable'
      , '@AppTable.TableName'
      , @TableName
      RETURN @Severity
   END

   SET @SQL = N'IF OBJECT_ID(''@View'') IS NOT NULL DROP VIEW ' + QUOTENAME(@ViewName)
   SET @SQL = REPLACE(@SQL, N'@View', QUOTENAME(@ViewName))
   EXECUTE sys.sp_executesql @SQL

   SET @Columns = N''

   -- If this is an _all with a matching base table and all sites are in the same db, just point
   -- to the base table.
   -- base table and the _all table, such as parms_mst and parms_mst_all.
   IF @BaseTable IS NOT NULL
   BEGIN
      SELECT @Columns = @Columns + CASE WHEN @Columns = N'' THEN N'' ELSE N',' END + QUOTENAME(sc.name) 
      FROM sys.columns sc
      INNER JOIN sys.columns sc2 ON
        sc.name = sc2.name
      WHERE sc.object_id = object_id(@TableName)
      AND sc2.object_id = object_id(@BaseTable)
      ORDER BY sc.column_id
      SET @SQL = N'CREATE VIEW ' + QUOTENAME(@ViewName) + N'
AS SELECT ' + @columns + N'
FROM ' + CASE WHEN dbo.AllSitesSameDb() = 1 THEN QUOTENAME(@BaseTable) ELSE QUOTENAME(@TableName) END
   END
   ELSE
   BEGIN
      SELECT @Columns = @Columns + CASE WHEN @Columns = N'' THEN N'' ELSE N',' END + QUOTENAME(sc.name)
      FROM sys.columns sc
      WHERE sc.object_id = object_id(@TableName)
      AND sc.name != @SiteColumn
      ORDER BY sc.column_id
      SET @SQL = N'CREATE VIEW ' + QUOTENAME(@ViewName) + N'
AS SELECT ' + @columns + N'
FROM ' + QUOTENAME(@TableName)
   END

   IF @IncludeSiteRefFilter = 1 AND @IsView = 0
   BEGIN
      IF @MasterSite IS NOT NULL AND LEN(@MasterSite) > 0
         SET @SQL = @SQL + N'
            WHERE ' + QUOTENAME(@SiteColumn) + N' = ''' + REPLACE(@MasterSite,N'''',N'''''') + ''''
      ELSE
         SET @SQL = @SQL + N'
            WHERE ' + QUOTENAME(@SiteColumn) + N' = CAST(CONTEXT_INFO() AS NVARCHAR(8))'
   END

   EXECUTE sys.sp_executesql @SQL

   RETURN @Severity
END
GO

